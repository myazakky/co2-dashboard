import json, strformat, sequtils, dom
include karax/prelude
import karax/kajax

include config

let headers = [
  ("authorization".cstring, (fmt"Bot {token}").cstring),
  ("Content-Type".cstring, "application/json".cstring)
]

type
  Room = ref object
    name*: string
    id*: string
    plotUrl*: string
    concentration*: int

proc update(room: Room) =
  proc onRecieve(status: int, response: cstring) =
    if status != 200: return

    let datas = ($response).parseJson
    var
      getImage = false
      getConc = false
    for data in datas:
      if (not getImage) and data["content"].str == "":
        room.plotUrl = data["attachments"][0]["url"].str
        getImage = true
      if (not getConc) and data["content"].str != "":
        room.concentration = data["content"].str.parseInt
        getConc = true

  ajaxGet(url = (fmt"https://discordapp.com/api/channels/{room.id}/messages").cstring,
          headers = headers,
          cont = onRecieve)

var rooms = nodes.map(proc(node: RoomConf): Room =
  Room(name: node.name, id: node.id, concentration: 0, plotUrl: "./demoplot.png"),
)

func isGood(room: Room): bool =
  room.concentration < 1000

func toHtml(room: Room): VNode =
  buildHtml(tdiv(class = "roomCard")):
    p(class = "roomName"): text room.name
    if room.isGood:
      p(class = "isGood good"): text "ã„ã„ã­ðŸ˜†"
    else:
      p(class = "isGood bad"): text "æ›æ°—ã—ã‚ˆðŸ˜‘"
    p(class = "ppm"): text $room.concentration & "ppm"
    img(src = room.plotUrl, class="plot")

func makeHeader(): VNode =
  buildHtml(tdiv(class = "header")):
    p(class = "title"): text "æ›æ°—çŠ¶æ³æ¸¬å®šä¸­"

func makeDescriptionBox(): VNode =
  buildHtml(tdiv(class = "descriptionBox")):
    p(class = "description"): text "å¤–æ°—ã¯å¤§ä½“500ppmã§ã™ï¼Ž"
    p(class = "description"): text "ç›®å®‰ã¨ã—ã¦1000ppmã‚’è¶…ãˆã‚‹ã¨æ›æ°—ãŒååˆ†ã§ãªã„ã¨åˆ¤æ–­ã—ã¦ã„ã¾ã™"
    p(class = "description"): text "ppm ã¯ ã“ã“ã§ã¯æ¿ƒåº¦ã®ã“ã¨ã§ã™ï¼Žparts per millionã®ç•¥ã§ï¼Œç™¾ä¸‡åˆ†çŽ‡ã®ã“ã¨ã§ã™ï¼Ž"

proc createDom(): VNode =
  buildHtml(tdiv):
    makeHeader()
    tdiv(class = "roomCards"):
      for room in rooms:
        room.toHtml
    tdiv(class = "information"):
      tdiv(class = "members"):
        p(class = "pasokonClab"): text "ãƒ‘ã‚½ã‚³ãƒ³éƒ¨ä½œæˆ"
        tdiv(class = "sponsor"):
          p(class = "logoText"): text "å”è³›"
          img(
            class = "airnormLogo",
            src = "./airnorm-logo.png"
          )
      makeDescriptionBox()

proc update(instance: KaraxInstance) =
  for room in rooms: room.update

var instance = setRenderer createDom

proc loop() = update(instance)
discard window.setInterval(loop, 10000)
